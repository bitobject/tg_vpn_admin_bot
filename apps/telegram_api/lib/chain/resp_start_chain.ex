defmodule TelegramApi.Chain.RespStartChain do
  use Telegex.Chain

  require Logger

  alias Core.Context, as: CoreContext
  alias TelegramApi.Telegram

  @impl Telegex.Chain
  def handle(%{message: %Telegex.Type.Message{text: "/start"}} = update, context) do
    with {:ok, chat_id} <- Telegram.get_chat_id(update),
         {:ok, from} <- Telegram.get_from(update),
         {:ok, username} <- Telegram.get_username(from) do
      Task.start(fn ->
        attrs = %{
          telegram_id: from.id,
          username: username,
          first_name: from.first_name,
          last_name: from.last_name,
          language_code: from.language_code,
          is_bot: from.is_bot
        }

        case CoreContext.create_or_update_user(attrs) do
          {:ok, user} ->
            send_welcome_message(chat_id, user)

          {:error, changeset} ->
            Logger.error(
              "Failed to create or update user in RespStartChain: #{inspect(changeset)}"
            )
        end
      end)
    else
      error ->
        Logger.error(
          "Could not extract required data from update in RespStartChain: #{inspect(error)}"
        )
    end

    {:stop, context}
  end

  def handle(_update, context), do: {:ok, context}

  defp send_welcome_message(chat_id, user) do
    text = welcome_text(user)
    keyboard = main_keyboard()

    Telegram.send_message(chat_id, text, parse_mode: "Markdown", reply_markup: keyboard)
  end

  defp welcome_text(user) do
    if user.inserted_at == user.updated_at do
      """
      üëã *–î–æ–±—Ä–æ –ø–æ–∂–∞–ª–æ–≤–∞—Ç—å, #{user.first_name}!*

      –≠—Ç–æ –±–æ—Ç –¥–ª—è —É–ø—Ä–∞–≤–ª–µ–Ω–∏—è –≤–∞—à–∏–º–∏ VPN-–ø–æ–¥–∫–ª—é—á–µ–Ω–∏—è–º–∏.

      –í–æ—Å–ø–æ–ª—å–∑—É–π—Ç–µ—Å—å –∫–Ω–æ–ø–∫–∞–º–∏ –Ω–∏–∂–µ –¥–ª—è –Ω–∞–≤–∏–≥–∞—Ü–∏–∏.
      """
    else
      """
      üëã *–° –≤–æ–∑–≤—Ä–∞—â–µ–Ω–∏–µ–º, #{user.first_name}!*

      –†–∞–¥ —Å–Ω–æ–≤–∞ –≤–∞—Å –≤–∏–¥–µ—Ç—å. –í–æ—Å–ø–æ–ª—å–∑—É–π—Ç–µ—Å—å –∫–Ω–æ–ø–∫–∞–º–∏ –Ω–∏–∂–µ –¥–ª—è –Ω–∞–≤–∏–≥–∞—Ü–∏–∏.
      """
    end
  end

  defp main_keyboard do
    %{
      keyboard: [
        [%{text: "–õ–∏—á–Ω—ã–π –∫–∞–±–∏–Ω–µ—Ç üíº"}],
        [%{text: "–ò–Ω—Å—Ç—Ä—É–∫—Ü–∏—è üìñ", url: "https://google.com"}, %{text: "–ü–æ–¥–¥–µ—Ä–∂–∫–∞ üÜò"}]
      ],
      resize_keyboard: true,
      one_time_keyboard: false
    }
  end
end
